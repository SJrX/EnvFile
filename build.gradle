plugins {
    id "java"
    id "idea"
    id "org.jetbrains.intellij" version "0.3.11"
    id "pl.allegro.tech.build.axion-release" version "1.9.3"
}

group "net.ashald"

scmVersion {
    tag {
        prefix = "v"
        versionSeparator = ""
    }
}

allprojects {
    repositories {
        mavenCentral()
    }
    project.version = scmVersion.version

    ext.jetbrains = [
            version : "2018.3",
            pycharm : "PythonCore:2018.3.183.4284.148",
            rubymine: "org.jetbrains.plugins.ruby:2018.3.20181121",

            goland  : "org.jetbrains.plugins.go:183.4284.36.1532",
            //goland  : "org.jetbrains.plugins.go:202.6397.20",
            nodejs  : "NodeJS"
    ]
}

intellij {
    version jetbrains.version
    pluginName "EnvFile"
    sameSinceUntilBuild false
    updateSinceUntilBuild false
    // TODO figure out why it needs to be here
    type "IU"
    plugins "NodeJS:183.4284.122", "JavaScriptLanguage", "JavaScriptDebugger"
    //plugins "NodeJS:203.5981.152", "JavaScriptLanguage", "JavaScriptDebugger"
    //plugins "NodeJS:202.6397.8", "JavaScriptLanguage", "JavaScriptDebugger"
}


dependencies {
    compile project(":envfile-products-idea")
    compile project(":envfile-products-pycharm")
    compile project(":envfile-products-rubymine")
    compile project(":envfile-products-goland")
    compile project(":envfile-products-nodejs")
}

wrapper {
    gradleVersion = "4.10.2"
}

def prj = ".idea/"

idea {
    module.iml {
      generateTo = file(prj)

      withXml {
          it.node.@type = "PLUGIN_MODULE"
          it.node.appendNode('component', [name: 'DevKit.ModuleBuildProperties', url: 'file://$MODULE_DIR$/src/main/resources/META-INF/plugin.xml'])
      }
    }
}

task setup {
    dependsOn ideaModule, ideaProject
    // overriding idea.project.outputFile changes relative path calculation so it's easier to just move a file
    doLast {
       copy {
          from '.'
          into prj
          include "${project.name}.ipr"
          rename { "modules.xml" }
       }
       project.delete "${project.name}.ipr"
     }
}
